# if x then
#    x + 1
# else
#    x + 2

# object = ((string, value) map -> value)

list[p] : p         { [$1] }
        | list[p] p { $1.append($2); $1 }
        ;

seplist[sep, p] : p                     { [$1] }
                | seplist[sep, p] sep p { $1.append($3); $1 }
                ;

START : <BOF> list[exp] <EOF> { mkblock($2) };

args : '(' seplist[',', arg] ')' { $2 }
     | '(' ')'  { [] }
     ;


# top level expression
exp : app
    | 'fun' args '=>' exp                      { mkfunc($2, $4) }
    | <ident> '=' exp                          { mkassign($1.value, $3) }
    | '{' list[exp] '}'                        { mkblock($2) }
    | 'if' exp 'then' exp 'else' exp           { mkif($2, $4, $6) }
    | '{' '}'                                  { const(None) }
    ;

arg : <ident> { $1.value };

app : atomexpr                      { $1 }
    | app '(' seplist[',', exp] ')' { call($1, $3) } # f(a, b, c)
    | app '(' ')'                   { call($1, ()) }
    ;

atomexpr : <int>       { const(int($1.value)) }
         | <float>     { const(float($1.value)) }
         | <str>       { const(unesc($1.value)) }
         | <ident>     { lookup($1.value) }
         | '(' exp ')' { $2 }
         ;


%%inline
from json.decoder import py_scanstring

def unesc(x, f=py_scanstring):
    """from the raw form of a double quoted string to a python string,
    e.g.,
        unesc('"asd"') == "asd"
    """
    return f(x, 1)[0]

def call(f, args):
    def ap(st):
        f_val = f(st)
        arg_vals = [arg(st) for arg in args]
        return f_val(*arg_vals)
    return ap

def mkfunc(params, body):
    def ap(st):
        def obj(*args):
            assert len(args) == len(params)
            st_ = st.copy()
            for arg, param in zip(args, params):
                st_[param] = arg
            return body(st_)
        return obj
    return ap

def mkassign(n, exp):
    def ap(st):
        st[n] = exp(st)
    return ap

def mkblock(xs):
    def ap(st):
        r = None
        for x in xs:
            r = x(st)
        return r
    return ap

def const(x):
    return lambda st: x

def lookup(n):
    # fun st -> Map.lookup n st
    return lambda st: st[n]

def mkif(cond, arm1, arm2):
    def ap(st):
        return (arm1 if cond(st) else arm2)(st)
    return ap
%%
